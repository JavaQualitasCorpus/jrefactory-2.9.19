/**
 *
 * Copyright (C) 1996, 1997 Sun Microsystems Inc.
 *
 * Use of this file and the system it is part of is constrained by the
 * file COPYRIGHT in the root directory of this system.  You may, however,
 * make any modifications you wish to this file.
 *
 * Java files generated by running JavaCC on this file (or modified versions
 * of this file) may be used in exactly the same manner as Java files
 * generated from any grammar developed by you.
 *
 * Author: Sriram Sankar
 * Date: 3/5/97
 * Author: Mike Atkinson - jdk 1.5 additions & amy other changes
 * Date: 19 June 2003
 *
 * This file contains a Java grammar and actions that implement a front-end.
 *
 */

options {
  STATIC=false;
  NODE_USES_PARSER=true;
  JAVA_UNICODE_ESCAPE=false;
  UNICODE_INPUT=true;
  VISITOR=true;
  MULTI=true;
  DEBUG_PARSER=false;
  DEBUG_LOOKAHEAD=false;
  DEBUG_TOKEN_MANAGER=false;
  OPTIMIZE_TOKEN_MANAGER=true;
}

PARSER_BEGIN(JavaParser)

public class JavaParser {

  private static final boolean TESTING = true;
  private static String target_jdk = "1.5.0";
  private static boolean jdk1_4 = true;
  private static boolean jdk1_5 = true;
  public static boolean generics = true;
  private static boolean annotations = true;
  static boolean useAssert = true;

  public static void setTargetJDK(String target) {
     target_jdk = target;
     jdk1_4 = "1.4.0".compareTo(target_jdk) <= 0;
     jdk1_5 = "1.5.0".compareTo(target_jdk) <= 0;
     generics = "1.5.0".compareTo(target_jdk) <= 0;
     annotations = "1.5.0".compareTo(target_jdk) <= 0;
     useAssert = "1.4.0".compareTo(target_jdk) <= 0;
  }
  static {
      setTargetJDK("1.5.0");
  }
  
  /** This lookahead is here rather than in the grammar to stop the
   *  generated tree containing an ASTExpressionLookahead class and
   *  the visitor having visit(ASTExpressionLookahead node, Object data);
   */
  private boolean expressionLookahead() {
     Token tok = getToken(1);
     if (tok.kind==COMMA) {
        tok = getToken(2);
        if (!(tok.kind==RPAREN || tok.kind==RBRACE)) {
            return true;
        }
     }
     return false;
  }


  /** This lookahead is here rather than in the grammar to stop the
   *  generated tree containing an ASTForLookahead class and
   *  the visitor having visit(ASTForLookahead node, Object data);
   */
  private boolean forLookahead() {
    int x = 0;
    Token tok = getToken(++x);
    if (tok.kind==FINAL) {
       tok = getToken(++x);
    }
    if (isPrimitiveType(tok)) {
       tok = getToken(++x);
    } else if (tok.kind==IDENTIFIER) {
       tok = getToken(++x);
       while (tok.kind==DOT) {
          tok = getToken(++x);
          if (tok.kind != IDENTIFIER) {
             return false;
          }
          tok = getToken(++x);
       }
    } else {
       return false;
    }
    if (tok.kind==LT) {
       while (true) {
          tok = getToken(++x);
          if (   tok.kind==LT || tok.kind==GT || tok.kind==RSIGNEDSHIFT || tok.kind==RUNSIGNEDSHIFT || tok.kind==DOT
              || tok.kind==COMMA || tok.kind==HOOK || tok.kind==SUPER || tok.kind==EXTENDS || tok.kind==IDENTIFIER) {
             // do nothing
          } else if (tok.kind==LBRACKET) {
             tok = getToken(++x);
             if (tok.kind!=RBRACKET) {
                return false;
             }
          } else {
             break;
          }
       }
       x--;
    } else if (tok.kind==LBRACKET) {
       while (tok.kind==LBRACKET) {
          tok = getToken(++x);
          if (tok.kind!=RBRACKET) {
             return false;
          }
          tok = getToken(++x);
       }
       if (tok.kind != IDENTIFIER) {
          return false;
       }
    }
    tok = getToken(++x);
    return (tok.kind==COLON);
  }


  /** This lookahead is here rather than in the grammar to stop the
   *  generated tree containing an ASTMethodDeclarationLookahead class and
   *  the visitor having visit(ASTMethodDeclarationLookahead node, Object data);
   */
  private boolean methodDeclarationLookahead() {
    int x = 0;
    Token tok = getToken(++x);
    while (    tok.kind==PUBLIC || tok.kind==PROTECTED || tok.kind==PRIVATE
            || tok.kind==STATIC || tok.kind==ABSTRACT || tok.kind==FINAL
            || tok.kind==NATIVE || tok.kind==SYNCHRONIZED || tok.kind==STRICTFP
            || tok.kind==ATTRIBUTE) {
       if (tok.kind==ATTRIBUTE) {
          int count=1;
          tok = getToken(++x);
          if (tok.kind!=IDENTIFIER) {
             return false;
          }
          tok = getToken(++x);
          if (tok.kind != LPAREN) {
             return false;
          }
          while (count>0) {
             tok = getToken(++x);
             if (tok.kind == LPAREN) {
                count++;
             } else if (tok.kind == RPAREN) {
                count--;
             }
          }
       }
       tok = getToken(++x);
    }

    if ( isPrimitiveType(tok) || tok.kind==VOID ) {
       tok = getToken(++x);
    } else if (tok.kind==IDENTIFIER) {
       tok = getToken(++x);
       while (tok.kind==DOT) {
          tok = getToken(++x);
          if (tok.kind != IDENTIFIER) {
             return false;
          }
          tok = getToken(++x);
       }
    } else if (tok.kind==LT) {
       return true;
    } else {
       return false;
    }
    if (tok.kind==LT) {
       while (true) {
          tok = getToken(++x);
          if (   tok.kind==LT || tok.kind==GT || tok.kind==RSIGNEDSHIFT || tok.kind==RUNSIGNEDSHIFT || tok.kind==DOT
              || tok.kind==COMMA || tok.kind==HOOK || tok.kind==SUPER || tok.kind==EXTENDS || tok.kind==IDENTIFIER) {
             // do nothing
          } else if (tok.kind==LBRACKET) {
             tok = getToken(++x);
             if (tok.kind!=RBRACKET) {
                return false;
             }
          } else {
             break;
          }
       }
       x--;
    } else if (tok.kind==LBRACKET) {
       while (tok.kind==LBRACKET) {
          tok = getToken(++x);
          if (tok.kind!=RBRACKET) {
             return false;
          }
          tok = getToken(++x);
       }
       if (tok.kind != IDENTIFIER) {
          return false;
       }
    }
    tok = getToken(++x);
    return (tok.kind==LPAREN);
  }


  /** This lookahead is here rather than in the grammar to stop the
   *  generated tree containing an ASTCastLookahead class and
   *  the visitor having visit(ASTCastLookahead node, Object data);
   */
   private boolean castLookahead() {
      int x = 0;
      Token tok = getToken(++x);
      if (tok.kind!=LPAREN) {
         return false;
      }
      tok = getToken(++x);
      if (tok.kind==IDENTIFIER) {
         tok = getToken(++x);
         while (true) {
            if (tok.kind==LT) {
               x = typeArguments(x);
               if (x<0) {
                  return false;
               }
               tok = getToken(x);  // load next token
            }
            if (tok.kind==LBRACKET) {
               while (tok.kind==LBRACKET) {
                  tok = getToken(++x);
                  if (tok.kind!=RBRACKET) {
                     return false;
                  }
                  tok = getToken(++x);
               }
            }
            if (tok.kind==RPAREN) {
               tok = getToken(++x);
               return castEnd(tok);
            } else if (tok.kind!=DOT) {
               return false;
            }
            tok = getToken(++x);
            if (isPrimitiveType(tok)) {
               tok = getToken(++x);
               if (tok.kind==LBRACKET) {
                  while (tok.kind==LBRACKET) {
                     tok = getToken(++x);
                     if (tok.kind!=RBRACKET) {
                        return false;
                     }
                     tok = getToken(++x);
                  }
               }
               return (tok.kind==RPAREN);
            } else if (tok.kind==IDENTIFIER) {
               tok = getToken(++x);
            }
            
         }
      } else if (isPrimitiveType(tok)) {
         tok = getToken(++x);
         if (tok.kind==LBRACKET) {
            while (tok.kind==LBRACKET) {
               tok = getToken(++x);
               if (tok.kind!=RBRACKET) {
                  return false;
               }
               tok = getToken(++x);
            }
         }
         return (tok.kind==RPAREN);
      }
      return false;
   }
   
   
   private boolean isPrimitiveType(Token tok) {
      return tok.kind==BOOLEAN || tok.kind==CHAR  || tok.kind==BYTE
          || tok.kind==SHORT   || tok.kind==INT   || tok.kind==LONG
          || tok.kind==FLOAT   || tok.kind==DOUBLE;
   }

   private int typeArguments(int x) {
      int level = 1;
      while (true) {
         Token tok = getToken(++x);
         if (tok.kind==GT) {
            --level;
            if (level==0) {
               return x+1;
            } else if (level < 0) {
               return -1; // this is probably a syntax error
            }
         } else if (tok.kind==RSIGNEDSHIFT) {
            level -= 2;
            if (level==0) {
               return x+1;
            } else if (level < 0) {
               return -1; // this is probably a syntax error
            }
         } else if (tok.kind==RUNSIGNEDSHIFT) {
            level -= 2;
            if (level==0) {
               return x+1;
            } else if (level < 0) {
               return -1; // this is probably a syntax error
            }
         } else if (tok.kind==LT) {
            level++;
         }
            
         if (tok.kind==RPAREN) {
            return -1;
         } else if (tok.kind==DOT) {
         } else if (tok.kind==HOOK || tok.kind==SUPER || tok.kind==EXTENDS || tok.kind==IDENTIFIER) {
            // do nothing
         }
      }
   }

            
   private boolean castEnd(Token tok) {
      return (   tok.kind==TILDE             || tok.kind==BANG            || tok.kind==LPAREN || tok.kind==IDENTIFIER
              || tok.kind==THIS              || tok.kind==SUPER           || tok.kind==NEW
              || tok.kind==INTEGER_LITERAL   || tok.kind==FLOATING_POINT_LITERAL
              || tok.kind==CHARACTER_LITERAL || tok.kind==STRING_LITERAL
              || tok.kind==TRUE              || tok.kind==FALSE           || tok.kind==NULL );
   }


  public static void main(String args[]) {
  }

}

PARSER_END(JavaParser)


/* WHITE SPACE */

SKIP :
{
 " " | "\t" | "\f"
 | "\u001a" { System.out.println("Warning: cntl-z found at end of file"); }: IN_ONLY_EOF
}

<IN_ONLY_EOF>
MORE :
{
    < ~[]  > { System.out.println("Error: no characters should follow cntl-z at end of file"); }
}

SPECIAL_TOKEN :
{
  "\n"
| "\r"
| "\r\n"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
|
  "/*<" : IN_CATEGORY_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN : { <SINGLE_LINE_COMMENT:  (~["\r","\n","\u001a"])* ("\r"|"\n"|"\r\n")? > : DEFAULT }

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_CATEGORY_COMMENT>
SPECIAL_TOKEN :
{
  <CATEGORY_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT,IN_CATEGORY_COMMENT>
MORE :
{
  < ~[] >
}

/* RESERVED WORDS AND LITERALS */

TOKEN :
{
  < ABSTRACT: "abstract" >
| < ASSERT: "assert" > { if (!JavaParser.useAssert) matchedToken.kind = IDENTIFIER; } 
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CLASS: "class" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" > { if (!JavaParser.generics) matchedToken.kind = IDENTIFIER; }
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package">
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < STRICTFP: "strictfp" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRANSIENT: "transient" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

/* LITERALS */

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ("\\u"
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ("\\u"
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
        	["0"-"9","A"-"F","a"-"f"]
          )
      )*
      "\""
  >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < VARARG: "..." >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < RSIGNEDSHIFT: ">>" >
| < RUNSIGNEDSHIFT: ">>>" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ATTRIBUTE: "@" >
}


/*****************************************
 * THE JAVA LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

/*
 * Program structuring syntax follows.
 */

ASTCompilationUnit CompilationUnit() :
{
    Token tok;
}
{
  [ PackageDeclaration() ]
  ( ImportDeclaration() )*
  ( TypeDeclaration() )*
  tok=<EOF>
  {
    jjtThis.addSpecial("EOF", tok.specialToken);
    return jjtThis;
  }
}

void PackageDeclaration() :
{
    Token tok;
}
{
  tok="package" {jjtThis.addSpecial("package", tok.specialToken);}
  Name()
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}

void ImportDeclaration() :
{
    Token tok = null;
    Token specialPoint = null;
    int count = 0;
}
{
  specialPoint="import" {jjtThis.addSpecial("import", specialPoint.specialToken);}
  [ specialPoint="static"
    {
      jjtThis.addSpecial("static", specialPoint.specialToken);
      jjtThis.setStaticImport(true);
    }
  ]
  Name()
    [
      specialPoint="." {jjtThis.addSpecial("period", specialPoint.specialToken);}
      tok="*" {jjtThis.addSpecial("star", tok.specialToken);}
    ]
  (
    specialPoint=";" 
    {
       count++;
       if (count==1) {
          jjtThis.addSpecial("semicolon", specialPoint.specialToken);
          jjtThis.setImportPackage(tok != null);
       }
    }
  )*
}

void TypeDeclaration() :
{
}
{
  LOOKAHEAD( ("abstract" | "public" | "strictfp" | ("@" Annotation()) )* "@" "interface" )
  AnnotationTypeDeclaration()
|
  LOOKAHEAD( ( "abstract" | "final" | "public" | "strictfp" | ("@" Annotation()) )* "class" )
  ClassDeclaration()
|
  LOOKAHEAD( ("public" | "protected" | "private" | "final" | ("@" Annotation()) )* "enum" )
  EnumDeclaration()
|
  InterfaceDeclaration()
}


/*
 * Declaration syntax follows.
 */

void ClassDeclaration() :
{
  Token tok;
  int count=0;
}
{
  (
    (tok="abstract" {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    (tok="final"    {jjtThis.setFinal(); jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="strictfp" {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="public"   {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++;} )
  )*
  UnmodifiedClassDeclaration()
  [ ";" ]
}

void UnmodifiedClassDeclaration() :
{
    Token tok;
    Token specialToken;
}
{
  specialToken="class" {jjtThis.addSpecial("class", specialToken.specialToken);}
  (
    tok=<IDENTIFIER>
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
  )
  [
    TypeParameters()
  ]
  [
    specialToken="extends"  {jjtThis.addSpecial("extends", specialToken.specialToken);}
    ClassOrInterfaceType()
  ]
  [
    specialToken="implements"  {jjtThis.addSpecial("implements", specialToken.specialToken);}
    GenericNameList()
  ]
  ClassBody()
}


void Annotation() :
{
  Token specialToken;
  Token tok;
}
{
  //specialToken="@" {jjtThis.addSpecial("@", specialToken.specialToken);}
  Identifier()
  [ specialToken="(" {jjtThis.setMarkerAnnotation(false); jjtThis.addSpecial("begin", specialToken.specialToken);}
    [ LOOKAHEAD(2) MemberValuePairs() | MemberValue() ]
    specialToken=")" {jjtThis.addSpecial("end", specialToken.specialToken);}
  ]
}


void MemberValuePairs() :
{
  Token specialToken;
  int count=0;
}
{
  MemberValuePair()
  ( specialToken="," {jjtThis.addSpecial("comma."+count++, specialToken.specialToken);}
     MemberValuePair()
  )*
}


void MemberValuePair() :
{
  Token specialToken;
}
{
  Identifier()
  specialToken="=" {jjtThis.addSpecial("=", specialToken.specialToken);}
  MemberValue()
}


void MemberValue() :
{
  Token tok;
}
{
  tok="@" Annotation()  {jjtThis.addSpecial("@", tok.specialToken); }
|
  MemberValueArrayInitializer()
|
  ConditionalExpression()
}


void MemberValueArrayInitializer() :
{
  Token specialToken;
  int count=0;
}
{
  specialToken="{" {jjtThis.addSpecial("{", specialToken.specialToken);}
  [ MemberValue()
    ( LOOKAHEAD(2)
      "," {jjtThis.addSpecial("comma."+count++, specialToken.specialToken);}
      MemberValue()
    )*
    [ "," ]
  ]
  specialToken="}" {jjtThis.addSpecial("}", specialToken.specialToken);}
}


void TypeParameters() :
{
  Token specialToken;
}
{
  specialToken="<"  {
    if (!jdk1_5) {
      throw new ParseException("Type Parameters are only available in JDK 1.5 or later generics" );
    }
    jjtThis.addSpecial("<", specialToken.specialToken);
  }
  TypeParameterList() 
  specialToken=">"  {jjtThis.addSpecial(">", specialToken.specialToken);}
}

void TypeParameterList() :
{
    Token generic;
    Token tok;
    int commaCount = 0;
}
{
    (
      TypeParameter()
    )
    (
      tok="," {jjtThis.addSpecial("comma."+commaCount, tok.specialToken); commaCount++; }
      TypeParameter()
    )*
}

void TypeParameter() :
{
    Token tok;
}
{
    (
      Identifier()
      [
        tok="extends" {jjtThis.addSpecial("extends.", tok.specialToken);}
        [ ClassOrInterfaceType() ]
        (
          tok="&" {jjtThis.addSpecial("and.", tok.specialToken);}
          [ ClassOrInterfaceType() ]
        )*
      ]
    )
}

void ReferenceTypeList() :
{
    Token tok;
    Token tok1;
    Token tok2;
    Token tok3;
    int commaCount = 0;
}
{
  tok="<" {
    if (!jdk1_5) {
      throw new ParseException("Generic reference types are only available in JDK 1.5 or later generics" );
    }
    jjtThis.addSpecial("<.", tok.specialToken);
  }
  ActualTypeArgument()
  (
    tok="," {jjtThis.addSpecial("comma."+commaCount, tok.specialToken); commaCount++; }
    ActualTypeArgument()
  )*
  (
    ( tok=">" {
      jjtThis.addSpecial(">.", tok.specialToken);} ) |
       /* This is an horendous hack caused by ">", ">>" and ">>>" all being tokens,
        * but in this case also being used as one, two or three individual tokens
        * ">", ">" ">" and ">" ">" ">".
        */
    ( tok=">>" { 
      jjtThis.addSpecial(">.", tok.specialToken);
      tok2=tok.next;
      tok1=Token.newToken(GT);
      tok.image=">";  tok.kind=GT;  tok.next=tok1;
      tok1.image=">"; tok1.kind=GT; tok1.next=tok2; } ) |
    ( tok=">>>" {
      jjtThis.addSpecial(">.", tok.specialToken);
      tok3=tok.next;
      tok1=Token.newToken(GT); tok2=Token.newToken(GT);
      tok.image=">";  tok.kind=GT;  tok.next=tok1;
      tok1.image=">"; tok1.kind=GT; tok1.next=tok2;
      tok2.image=">"; tok2.kind=GT; tok2.next=tok3; } )
   )
}

void ActualTypeArgument() :
{
    Token tok;
}
{
  // Wildcard
  tok="?" {
      jjtThis.addSpecial("?", tok.specialToken);
      jjtThis.setWildcard();
    }
    [
      // WildCardBounds
      (
        tok="extends" { jjtThis.setExtends(); jjtThis.addSpecial("entends", tok.specialToken); }
        |
        tok="super" { jjtThis.setSuper(); jjtThis.addSpecial("super", tok.specialToken); }
      )
      ReferenceType()
    ]  

|
  ReferenceType()
}


void ClassBody() :
{
    Token tok;
}
{
  tok="{" {jjtThis.addSpecial("begin", tok.specialToken);}
  ( ClassBodyDeclaration() )*
  tok="}" {jjtThis.addSpecial("end", tok.specialToken);}
}

void NestedClassDeclaration() :
{
    Token tok;
    int count=0;
}
{
  (
    (tok="static"    {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="strictfp"  {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="abstract"  {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    (tok="final"     {jjtThis.setFinal(); jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="protected" {jjtThis.setProtected(); jjtThis.addSpecial("protected", tok.specialToken);} ) |
    (tok="private"   {jjtThis.setPrivate(); jjtThis.addSpecial("private", tok.specialToken);} ) |
    (tok="@" Annotation()  {jjtThis.addSpecial("@."+count, tok.specialToken); count++;} )
  )*
  UnmodifiedClassDeclaration()
  [ ";" ]
}

ASTJSPBody JSPBody() :
{}
{
  ( ClassBodyDeclaration() )*
  { return jjtThis; }
}

void ClassBodyDeclaration() :
{}
{
   (
     LOOKAHEAD( ["static" ] "{" )
     Initializer()
   |
     LOOKAHEAD( ( "static" | "abstract" | "final" | "public" | "protected" | "private" | "strictfp" | ("@" Annotation()) )*  "class" )
     NestedClassDeclaration()
   |
     LOOKAHEAD( ( "static" | "abstract" | "final" | "public" | "protected" | "private" | "strictfp" | ("@" Annotation()) )* "interface" )
     NestedInterfaceDeclaration()
   |
     LOOKAHEAD( ( "public" | "protected" | "private" | ( "@" Annotation()) )* [ TypeParameters() ] Name() [ TypeArguments() ] "(" )
     ConstructorDeclaration()
   |
     LOOKAHEAD( {methodDeclarationLookahead()} )
     MethodDeclaration()
   |
     LOOKAHEAD( ("static" | "public" | "protected" | "private" | "final" | ("@" Annotation()) )* "enum" )
     EnumDeclaration()
   |
     FieldDeclaration()
   )
}

// enumeration from jdk1.5
void EnumDeclaration() :
{
    Token tok;
    Token specialToken;
    int elementCount = 0;
    int count = 0;
}
{
  (
    (tok="static"    {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="final"     {jjtThis.setFinal(); jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="protected" {jjtThis.setProtected(); jjtThis.addSpecial("protected", tok.specialToken);} ) |
    (tok="private"   {jjtThis.setPrivate(); jjtThis.addSpecial("private", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++;} )
  )*
  tok="enum" { jjtThis.addSpecial("enum", tok.specialToken); }
  Identifier()
  [
    specialToken="implements"  {jjtThis.addSpecial("implements", specialToken.specialToken);}
    GenericNameList()
  ]
  tok = "{"  {jjtThis.addSpecial("begin", tok.specialToken);}
  EnumElement()
  (
    tok = ","
    EnumElement()
    {
      jjtThis.addSpecial("comma." + elementCount, tok.specialToken);
      elementCount++;
    }
  )*
  [ ";" ( ClassBodyDeclaration() )* ]
  tok = "}" {jjtThis.addSpecial("end", tok.specialToken);}
  ( ";" )*
}

void Identifier() :
{
    Token tok;
}
{
    tok = <IDENTIFIER>
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
}

void EnumElement() :
{
    Token tok;
}
{
    tok = <IDENTIFIER>
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
    [ Arguments() ]
    [ ClassBody() ]
}


void AnnotationTypeDeclaration() :
{
    Token tok;
    int count=0;
}
{
   
  (
    ( tok="abstract"  {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    ( tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    ( tok="strictfp"  {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    ( tok="@"
      (
         ( tok="interface"  {jjtThis.addSpecial("@interface", tok.specialToken);} ) |
         ( Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++;} )
      )
    )
  )*
  //tok="@" {jjtThis.addSpecial("@", tok.specialToken);}
  //tok="interface"  {jjtThis.addSpecial("interface", tok.specialToken);}
  tok=<IDENTIFIER>
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
  tok="{" {jjtThis.addSpecial("}", tok.specialToken);}
  (
    AnnotationTypeMemberDeclaration()
    //[ tok=";" {jjtThis.addSpecial(";", tok.specialToken);} ]
  )*
  tok="}" {jjtThis.addSpecial("}", tok.specialToken);}
}


void AnnotationTypeMemberDeclaration() :
{
    Token tok;
}
{
  LOOKAHEAD( [ "public" | "abstract" ] Type() Identifier() "(" )
  AnnotationMethodDeclaration()
|
  LOOKAHEAD( [ "public" | "static" | "final" ] Type() )
  ConstantDeclaration()
|
  LOOKAHEAD( ["abstract" | "public" | "strictfp" ] "@" "interface" )
  AnnotationTypeDeclaration()
|
  LOOKAHEAD( ( "abstract" | "final" | "public" | "strictfp" | ("@" Annotation()) )* "class" )
  ClassDeclaration()
|
  LOOKAHEAD( ("public" | "protected" | "private" | "final" | ("@" Annotation()) )* "enum" )
  EnumDeclaration()
|
  InterfaceDeclaration()
}


void AnnotationMethodDeclaration() :
{
    Token tok;
}
{
  (
    (tok="public"       {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="abstract"     {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} )
  )*
  Type()
  Identifier()
  tok="(" {jjtThis.addSpecial("(", tok.specialToken);}
  tok=")" {jjtThis.addSpecial(")", tok.specialToken);}
  [
    tok="default" {jjtThis.addSpecial("detault", tok.specialToken);}
    MemberValue()
  ]
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}


void ConstantDeclaration() :
{
    Token tok;
    int commaCount = 0;
}
{
  (
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="static"    {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="final"     {jjtThis.setFinal();  jjtThis.addSpecial("final", tok.specialToken);} )
  )
  Type() VariableDeclarator()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++; }
    VariableDeclarator()
  )*
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}


void InterfaceDeclaration() :
{
    Token tok;
    int count=0;
}
{
  (
    (tok="abstract"  {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="strictfp"  {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++; } )
  )*
  UnmodifiedInterfaceDeclaration()
  [ ";" ]
}

void NestedInterfaceDeclaration() :
{
    Token tok;
    int count=0;
}
{
  (
    (tok="static"    {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="abstract"  {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    (tok="strictfp"  {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="final"     {jjtThis.setFinal(); jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="protected" {jjtThis.setProtected(); jjtThis.addSpecial("protected", tok.specialToken);} ) |
    (tok="private"   {jjtThis.setPrivate(); jjtThis.addSpecial("private", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++;} )
  )*
  UnmodifiedInterfaceDeclaration()
  [ ";" ]
}

void UnmodifiedInterfaceDeclaration() :
{
    Token specialToken;
    Token tok;
}
{
  specialToken="interface" {jjtThis.addSpecial("interface", specialToken.specialToken);}
  tok=<IDENTIFIER>
  {
    jjtThis.setName(tok.image);
    {jjtThis.addSpecial("id", tok.specialToken);}
  }
  [ TypeParameters() ]
  [
    specialToken="extends" {jjtThis.addSpecial("extends", specialToken.specialToken);}
    GenericNameList()
  ]
  InterfaceBody()
}

void InterfaceBody() :
{
	Token tok;
}
{
  tok="{" {jjtThis.addSpecial("begin", tok.specialToken);}
  ( InterfaceMemberDeclaration() )*
  tok="}" {jjtThis.addSpecial("end", tok.specialToken);}
}

void InterfaceMemberDeclaration() :
{}
{
  LOOKAHEAD( ( "static" | "abstract" | "final" | "public" | "protected" | "private" | "strictfp" | ("@" Annotation()) )* "class" )
  NestedClassDeclaration()
|
  LOOKAHEAD( ( "static" | "abstract" | "final" | "public" | "protected" | "private" | "strictfp" | ("@" Annotation()) )* "interface" )
  NestedInterfaceDeclaration()
|
  LOOKAHEAD( {methodDeclarationLookahead()} )
  MethodDeclaration()
|
  FieldDeclaration()
}

void FieldDeclaration() :
{
    Token tok;
    int commaCount = 0;
    int count = 0;
}
{
  (
    (tok="static"    {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="transient" {jjtThis.addModifier(tok.image); jjtThis.addSpecial("transient", tok.specialToken);} ) |
    (tok="volatile"  {jjtThis.addModifier(tok.image); jjtThis.addSpecial("volatile", tok.specialToken);} ) |
    (tok="strictfp"  {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="final"     {jjtThis.setFinal();  jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="public"    {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="protected" {jjtThis.setProtected(); jjtThis.addSpecial("protected", tok.specialToken);} ) |
    (tok="private"   {jjtThis.setPrivate(); jjtThis.addSpecial("private", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.setPrivate(); jjtThis.addSpecial("@."+count, tok.specialToken); count++; } )
  )*
  Type() VariableDeclarator()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++; }
    VariableDeclarator()
  )*
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
  (";")*
}

void VariableDeclarator() :
{
    Token tok;
}
{
  VariableDeclaratorId()
  [
    tok="=" {jjtThis.addSpecial("equals", tok.specialToken);}
    VariableInitializer()
  ]
}

void VariableDeclaratorId() :
{
    Token tok;
    int arrayCount = 0;
}
{
  tok=<IDENTIFIER>
  {
    jjtThis.addSpecial("id", tok.specialToken);
    jjtThis.setName(tok.image);
  }
  (
    "[" "]"
    {arrayCount++;}
  )*
  {
    jjtThis.setArrayCount(arrayCount);
  }
}

void VariableInitializer() :
{}
{
  ArrayInitializer()
|
  Expression()
}

void ArrayInitializer() :
{
    Token tok;
    int commaCount = 0;
}
{
  tok="{" { jjtThis.addSpecial("begin", tok.specialToken); }
  [
    VariableInitializer()
    (
      LOOKAHEAD(2)
      tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++;}
      VariableInitializer()
    )*
  ]
  [
    tok=","
    {
      jjtThis.addSpecial("comma." + commaCount, tok.specialToken);
      commaCount++;
      jjtThis.setFinalComma(true);
    }
  ]
  tok="}" { jjtThis.addSpecial("end", tok.specialToken); }
}

void MethodDeclaration() :
{
    Token tok;
    int count=0;
}
{
  (
    (tok="public"       {jjtThis.setPublic(); jjtThis.addSpecial("public", tok.specialToken);} ) |
    (tok="protected"    {jjtThis.setProtected(); jjtThis.addSpecial("protected", tok.specialToken);} ) |
    (tok="private"      {jjtThis.setPrivate(); jjtThis.addSpecial("private", tok.specialToken);} ) |
    (tok="static"       {jjtThis.setStatic(); jjtThis.addSpecial("static", tok.specialToken);} ) |
    (tok="abstract"     {jjtThis.setAbstract(); jjtThis.addSpecial("abstract", tok.specialToken);} ) |
    (tok="final"        {jjtThis.setFinal(); jjtThis.addSpecial("final", tok.specialToken);} ) |
    (tok="native"       {jjtThis.addModifier(tok.image); jjtThis.addSpecial("native", tok.specialToken);} ) |
    (tok="strictfp"     {jjtThis.setStrict(); jjtThis.addSpecial("strictfp", tok.specialToken);} ) |
    (tok="synchronized" {jjtThis.setSynchronized(); jjtThis.addSpecial("synchronized", tok.specialToken);} ) |
    (tok="@" Annotation() {jjtThis.addSpecial("@."+count, tok.specialToken); count++; } )
  )*
  [ TypeParameters() ] ResultType() MethodDeclarator()
  [
    tok="throws" {jjtThis.addSpecial("throws", tok.specialToken);}
    NameList()
  ]
  (
    Block() |
    (tok=";"  {jjtThis.addSpecial("semicolon", tok.specialToken);})
  )
  //  Ignore the extra semicolons
  ( ";" )*
}

void MethodDeclarator() :
{
    Token tok;
    int arrayCount = 0;
}
{
  tok=<IDENTIFIER>
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("id", tok.specialToken);
  }
  FormalParameters()
  (
      tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
      tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
      {arrayCount++;}
  )*
  {
    jjtThis.setArrayCount(arrayCount);
  }
}

void FormalParameters() :
{
    Token tok;
    int commaCount = 0;
}
{
  tok="(" {jjtThis.addSpecial("begin", tok.specialToken);}
  [
    FormalParameter()
    (
      tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken);}
      FormalParameter()
      {commaCount++;}
    )*
  ]
  tok=")" {jjtThis.addSpecial("end", tok.specialToken);}
}

void FormalParameter() :
{
    Token tok = null;
    int count=0;
}
{
  (
    (tok="@" Annotation()  {jjtThis.addSpecial("@."+count, tok.specialToken); count++; }) |
    (tok="final" {jjtThis.setUsingFinal(true); jjtThis.addSpecial("final", tok.specialToken); } )
  )*
  Type() 
  [ tok="..." {jjtThis.setVarArg(true); jjtThis.addSpecial("vararg", tok.specialToken);} ]
  VariableDeclaratorId()
}

void ConstructorDeclaration() :
{
    Token tok;
    Token mod;
    int count = 0;
}
{
  (
    (mod="@" Annotation() {jjtThis.addSpecial("@."+count, mod.specialToken); count++; } ) |
    (mod="public"         {jjtThis.setPublic(); jjtThis.addSpecial("public", mod.specialToken);} ) |
    (mod="protected"      {jjtThis.setProtected(); jjtThis.addSpecial("protected", mod.specialToken);} ) |
    (mod="private"        {jjtThis.setPrivate(); jjtThis.addSpecial("private", mod.specialToken);} )
  )*
  [ TypeParameters() ]
  tok=<IDENTIFIER>
  {
    jjtThis.addSpecial("id", tok.specialToken);
    jjtThis.setName(tok.image);
  }
  [ TypeArguments() ]
  FormalParameters()
  [
    tok="throws" {jjtThis.addSpecial("throws", tok.specialToken);}
    NameList()
  ]
  tok="{" {jjtThis.addSpecial("begin", tok.specialToken);}
    [
      LOOKAHEAD(  ([TypeArguments()] "this" "(" )
                | ([<IDENTIFIER> "."] [ TypeArguments() ] tok="super" "(" ))
      ExplicitConstructorInvocation()
    ]
    ( BlockStatement() )*
  tok="}" {jjtThis.addSpecial("end", tok.specialToken);}
  [
    ";"
  ]
}

void ExplicitConstructorInvocation() :
{
    Token tok;
    Token semicolon;
    Token dot = null;
}
{
(
  LOOKAHEAD([TypeArguments()] "this" "(" )
  [ TypeArguments() ]   // new
  tok="this" Arguments() semicolon=";" {((ASTExplicitConstructorInvocation)jjtThis).setIsThis();}
|
  [ LOOKAHEAD(PrimaryExpression() "." ) PrimaryExpression() dot="." { jjtThis.addSpecial(".", dot.specialToken); } ]
  [ Identifier() dot="." { jjtThis.addSpecial(".2", dot.specialToken); } ]
  [ TypeArguments() ]
  tok="super" Arguments() semicolon=";"
  {
    ((ASTExplicitConstructorInvocation)jjtThis).setIsSuper();
  }
)
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("explicit", tok.specialToken);
    jjtThis.addSpecial("semicolon", semicolon.specialToken);
  }
}

void Initializer() :
{
    Token tok = null;
}
{
  [ tok = "static" ] Block()
  {
    jjtThis.setUsingStatic(tok != null);
    if (tok != null) {
      jjtThis.addSpecial("static", tok.specialToken);
    }
  }
  ( ";" )*
}


/*
 * Type, name and expression syntax follows.
 */


void Type():
{
}
{
  (
    LOOKAHEAD(2)
    ReferenceType()
  |
    PrimitiveType()
  )
}

void ReferenceType() :
{
    int arrayCount = 0;
    Token tok;
}
{
  (
    (
      ClassOrInterfaceType()
      (
        tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
        tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
        {arrayCount++;}
      )*
    )
    |
    (
      PrimitiveType()
      (
        tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
        tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
        {arrayCount++;}
      )+
    )
  )
  {
    jjtThis.setArrayCount(arrayCount);
  }
}

void TypeArguments() :
{
    Token tok;
    Token tok1;
    Token tok2;
    Token tok3;
    int commaCount = 0;
}
{
  tok="<" {
    if (!jdk1_5) {
      throw new ParseException("Generic type arguments are only available in JDK 1.5 or later generics" );
    }
    jjtThis.addSpecial("<.", tok.specialToken);
  }
  ActualTypeArgument()
  (
    LOOKAHEAD(2)
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++; }
    ActualTypeArgument()
  )*
  (
    (tok=">" {jjtThis.addSpecial(">.", tok.specialToken);} ) |
     
    /* This is an horendous hack caused by ">", ">>" and ">>>" all being tokens,
     * but in this case also being used as one, two or three individual tokens
     * ">", ">" ">" and ">" ">" ">".
     */
    ( tok=">>" { 
        jjtThis.addSpecial(">.", tok.specialToken);
        tok2=tok.next;
        tok1=Token.newToken(GT);
        tok.image=">";  tok.kind=GT;  tok.next=tok1;
        tok1.image=">"; tok1.kind=GT; tok1.next=tok2; } ) |
    ( tok=">>>" {
        jjtThis.addSpecial(">.", tok.specialToken);
        tok3=tok.next;
        tok1=Token.newToken(GT); tok2=Token.newToken(GT);
        tok.image=">";  tok.kind=GT;  tok.next=tok1;
        tok1.image=">"; tok1.kind=GT; tok1.next=tok2;
        tok2.image=">"; tok2.kind=GT; tok2.next=tok3; } )
  )
}

void PrimitiveType() :
{
    Token tok;
}
{
(
    tok="boolean"
  |
    tok="char"
  |
    tok="byte"
  |
    tok="short"
  |
    tok="int"
  |
    tok="long"
  |
    tok="float"
  |
    tok="double"
)
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("primitive", tok.specialToken);
  }
}

void ResultType() :
{
    Token tok;
}
{
  (tok="void" {jjtThis.addSpecial("primitive", tok.specialToken);})
|
  Type()
}

void Name() :
/*
 * A lookahead of 2 is required below since "Name" can be followed
 * by a ".*" when used in the context of an "ImportDeclaration".
 */
{
    Token tok;
    Token period;
    int count = 0;
}
{
  tok=<IDENTIFIER> {jjtThis.addSpecial("id" + count, tok.specialToken);}
  {
    if (TESTING) {
        jjtThis.testingOnly__setBeginLine( tok.beginLine);
        jjtThis.testingOnly__setBeginColumn( tok.beginColumn);
    }
    jjtThis.addNamePart(tok.image);
    count++;
  }
  (
    LOOKAHEAD(2)
    period="."
    tok=<IDENTIFIER>
    {
      jjtThis.addNamePart(tok.image);
      jjtThis.addSpecial("period" + count, period.specialToken);
      jjtThis.addSpecial("id" + count, tok.specialToken);
      count++;
    }
  )*
}



void ClassOrInterfaceType() :
{
    Token period;
    int count = 0;
}
{
  Identifier()
  [ TypeArguments() ]
  (
    LOOKAHEAD( "." <IDENTIFIER> )
    period="."
    Identifier()
    [ TypeArguments() ]
    {
      jjtThis.addSpecial("period." + count, period.specialToken);
      count++;
    }
  )*
}


void NameList() :
{
    Token tok;
    int commaCount = 0;
}
{
  Name()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken);}
    Name()
    {commaCount++;}
  )*
}


void GenericNameList() :
{
    Token tok;
    int commaCount = 0;
}
{
  ClassOrInterfaceType()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken);}
    ClassOrInterfaceType()
    {commaCount++;}
  )*
}


/*
 * Expression syntax follows.
 */

void Expression() :
/*
 * This expansion has been written this way instead of:
 *   Assignment() | ConditionalExpression()
 * for performance reasons.
 * However, it is a weakening of the grammar for it allows the LHS of
 * assignments to be any conditional expression whereas it can only be
 * a primary expression.  Consider adding a semantic predicate to work
 * around this.
 */
{}
{
  ConditionalExpression()
  [
    AssignmentOperator() Expression()
  ]
}

void AssignmentOperator() :
{
    Token tok;
}
{
  (
    tok="="    |
    tok="*="   |
    tok="/="   |
    tok="%="   |
    tok="+="   |
    tok="-="   |
    tok="<<="  |
    tok=">>="  |
    tok=">>>=" |
    tok="&="   |
    tok="^="   |
    tok="|="
  )
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("operator", tok.specialToken);
  }
}

void ConditionalExpression() :
{
    Token tok;
}
{
  ConditionalOrExpression()
  [
    tok="?" {jjtThis.addSpecial("?", tok.specialToken);}
    Expression()
    tok=":" {jjtThis.addSpecial(":", tok.specialToken);}
    ConditionalExpression()
  ]
}

void ConditionalOrExpression() :
{
    Token tok;
    int count = 0;
}
{
  ConditionalAndExpression()
  (
    tok="||" {jjtThis.addSpecial("operator." + count, tok.specialToken); count++;}
    ConditionalAndExpression()
  )*
}

void ConditionalAndExpression() :
{
    Token tok;
    int count = 0;
}
{
  InclusiveOrExpression()
  (
    tok="&&" {jjtThis.addSpecial("operator." + count, tok.specialToken); count++;}
    InclusiveOrExpression()
  )*
}

void InclusiveOrExpression() :
{
    Token tok;
    int count = 0;
}
{
  ExclusiveOrExpression()
  (
    tok="|" {jjtThis.addSpecial("operator." + count, tok.specialToken); count++;}
    ExclusiveOrExpression()
  )*
}

void ExclusiveOrExpression() :
{
    Token tok;
    int count = 0;
}
{
  AndExpression()
  (
    tok="^" {jjtThis.addSpecial("operator." + count, tok.specialToken); count++;}
    AndExpression()
  )*
}

void AndExpression() :
{
    Token tok;
    int count = 0;
}
{
  EqualityExpression()
  (
    tok="&" {jjtThis.addSpecial("operator." + count, tok.specialToken); count++;}
    EqualityExpression()
  )*
}

void EqualityExpression() :
{
    Token tok;
    int count = 0;
}
{
  InstanceOfExpression()
  (
    ( tok="==" | tok="!=" )
    InstanceOfExpression()
    {
        jjtThis.addName(tok.image);
        jjtThis.addSpecial("operator." + count, tok.specialToken);
        count++;
    }
  )*
}

void InstanceOfExpression() :
{
    Token tok;
}
{
  RelationalExpression()
  [
    tok="instanceof"
    ReferenceType()
    {jjtThis.addSpecial("operator.0", tok.specialToken);}
  ]
}

void RelationalExpression() :
{
    Token tok;
    int count = 0;
}
{
  ShiftExpression()
  (
    (
      tok="<"  |
      tok=">"  |
      tok="<=" |
      tok=">="
    )
    {
      jjtThis.addName(tok.image);
      jjtThis.addSpecial("operator." + count, tok.specialToken);
      count++;
    }
    ShiftExpression()
  )*
}

void ShiftExpression() :
{
    Token tok;
    int count = 0;
}
{
  AdditiveExpression()
    (
      (
        tok="<<" |
        tok=">>" |
        tok=">>>"
      )
      {
        jjtThis.addName(tok.image);
        jjtThis.addSpecial("operator." + count, tok.specialToken);
        count++;
      }
      AdditiveExpression()
    )*
}

void AdditiveExpression() :
{
    Token tok;
    int count = 0;
}
{
  MultiplicativeExpression()
    (
      (
        tok="+" |
        tok="-"
      )
      {
        jjtThis.addName(tok.image);
        jjtThis.addSpecial("operator." + count, tok.specialToken);
        count++;
      }
      MultiplicativeExpression()
    )*
}

void MultiplicativeExpression() :
{
    Token tok;
    int count = 0;
}
{
  UnaryExpression()
    (
      (
        tok="*" |
        tok="/" |
        tok="%"
      )
      {
        jjtThis.addName(tok.image);
        jjtThis.addSpecial("operator." + count, tok.specialToken);
        count++;
      }
      UnaryExpression()
    )*
}

void UnaryExpression() :
{
    Token tok;
}
{
  (
    (
      tok="+" |
      tok="-"
    )
    UnaryExpression()
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("operator", tok.specialToken);
    }
  )
|
  PreIncrementExpression()
|
  PreDecrementExpression()
|
  UnaryExpressionNotPlusMinus()
}

void PreIncrementExpression() :
{
    Token tok;
}
{
  tok="++" PrimaryExpression() {jjtThis.addSpecial("operator", tok.specialToken);}
}

void PreDecrementExpression() :
{
    Token tok;
}
{
  tok="--" PrimaryExpression() {jjtThis.addSpecial("operator", tok.specialToken);}
}

void UnaryExpressionNotPlusMinus() :
{
    Token tok;
}
{
  LOOKAHEAD( {castLookahead()} )
  CastExpression()
|
  (
    (
      tok="~" |
      tok="!"
    )
    UnaryExpression()
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("operator", tok.specialToken);
    }
  )
|
  PostfixExpression()
}


void PostfixExpression() :
{
    Token tok = null;
}
{
  PrimaryExpression() [ tok="++" | tok="--" ]
  {
    if (tok != null) {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("operator", tok.specialToken);
    }
  }
}

void CastExpression() :
{
    Token tok;
}
{
  tok="(" {jjtThis.addSpecial("begin", tok.specialToken);}
  Type()
  tok=")" {jjtThis.addSpecial("end", tok.specialToken);}
  
  UnaryExpression()  // fix bug 758816 UnaryExpressionNotPlusMinus()
}

void PrimaryExpression() :
{}
{
  PrimaryPrefix()
  ( LOOKAHEAD( ("." "this") | ("." "super") | ("." "new") | "(" | "[" | ( "." "<") | ("." [ ReferenceTypeList() ] <IDENTIFIER>) | ReferenceTypeList() )
    PrimarySuffix()
  )*
}

void PrimaryPrefix() :
{
  Token tok;
  Token superTok;
  String name = "";
  int count = 0;
}
{
  Literal()
|
  tok="this"
  {
    jjtThis.setUsesThisModifier();
    jjtThis.setName("this");
    jjtThis.addSpecial("this", tok.specialToken);
  }
|
  LOOKAHEAD( (<IDENTIFIER> ".")* "super" "." )
  (
     tok=<IDENTIFIER> "."
     {
     	name = name + tok.image + ".";
	    jjtThis.addSpecial("this." + count, tok.specialToken);
	    count++;
     }
  )*
  superTok="super" "." tok=<IDENTIFIER>
  {
    jjtThis.setUsesSuperModifier();
    jjtThis.setCount(count);
    name = name + "super." + tok.image;
    jjtThis.setName(name);
    jjtThis.addSpecial("this", superTok.specialToken);
    jjtThis.addSpecial("id", tok.specialToken);
  }
|
  tok="(" {jjtThis.addSpecial("begin", tok.specialToken);}
  Expression()
  tok=")" {jjtThis.addSpecial("end", tok.specialToken);}
|
  AllocationExpression()
|
  LOOKAHEAD( ResultType() "." "class" )
  ResultType() "." "class"
|
  Name() //? ClassOrInterfaceType()
}

void PrimarySuffix() :
{
    Token tok;
    Token tok1;
    Token tok2;
    Token tok3;
    Token generic;
}
{
  tok="." {jjtThis.addSpecial("dot", tok.specialToken);}
  (
    tok="this"
    {
       jjtThis.setName("this");
       jjtThis.addSpecial("id", tok.specialToken);
    }
  |
    tok="super"
    {
      jjtThis.setName("super");
      jjtThis.addSpecial("id", tok.specialToken);
    }
  |
    AllocationExpression()
  |
    [ 
      ReferenceTypeList()
    ]
    tok=<IDENTIFIER>
    {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
  )
|
  tok="[" {jjtThis.addSpecial("[", tok.specialToken);}
  Expression()
  tok="]" {jjtThis.addSpecial("]", tok.specialToken);}
|
  Arguments() {((ASTPrimarySuffix)jjtThis).setIsArguments();}
|
  ReferenceTypeList()
}

void Literal() :
{
    Token tok = null;
}
{
  (
    tok=<INTEGER_LITERAL> |
    tok=<FLOATING_POINT_LITERAL> |
    tok=<CHARACTER_LITERAL> |
    tok=<STRING_LITERAL> |
    BooleanLiteral() |
    NullLiteral()
  )
  {
    if (tok != null) {
      jjtThis.setName(tok.image);
      jjtThis.addSpecial("id", tok.specialToken);
    }
  }
}

void BooleanLiteral() :
{
    Token tok;
}
{
  (
    tok="true" |
    tok="false"
  )
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("id", tok.specialToken);
  }
}

void NullLiteral() :
{
    Token tok;
}
{
  tok="null"
  {
    jjtThis.addSpecial("id", tok.specialToken);
  }
}

void Arguments() :
{
    Token tok;
}
{
  tok="(" {jjtThis.addSpecial("begin", tok.specialToken);}
  [ ArgumentList() ] [ tok=","  {jjtThis.addSpecial("comma", tok.specialToken);} ]
  tok=")" {jjtThis.addSpecial("end", tok.specialToken);}
}

void ArgumentList() :
{
    Token tok;
    int commaCount = 0;
}
{
  Expression()
  (
    LOOKAHEAD({expressionLookahead()})
    tok=","  {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++;}
    Expression()
  )*
}

void AllocationExpression() :
{
    Token tok;
}
{

  tok="new" {jjtThis.addSpecial("id", tok.specialToken);}
  (
    LOOKAHEAD(ClassOrInterfaceType())
    ClassOrInterfaceType()
    (
      ArrayDimsAndInits()
    |
      Arguments() [ ClassBody() ]
    )
  |
    PrimitiveType()
    ArrayDimsAndInits()
  )
}

/*
 * The third LOOKAHEAD specification below is to parse to PrimarySuffix
 * if there is an expression between the "[...]".
 */
void ArrayDimsAndInits() :
{
    Token tok;
    int arrayCount = 0;
}
{
  (
    // FIXME? LOOKAHEAD looks wrong??
    LOOKAHEAD(2)
    (
      LOOKAHEAD(2)
      tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
      Expression()
      tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
      {arrayCount++;}
    )+
    (
      LOOKAHEAD(4)
      tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
      tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
      {arrayCount++;}
    )*
   |
    (
      tok="[" {jjtThis.addSpecial("[." + arrayCount, tok.specialToken);}
      tok="]" {jjtThis.addSpecial("]." + arrayCount, tok.specialToken);}
      {arrayCount++;}
    )+
    ArrayInitializer()
  )
  {
    jjtThis.setArrayCount(arrayCount);
  }
}


/*
 * Statement syntax follows.
 */

void Statement() :
{
    Token tok;
}
{
  AssertionStatement()
|
  LOOKAHEAD(2)
  LabeledStatement()
|
  Block()
|
  EmptyStatement()
|
  StatementExpression() tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
|
  SwitchStatement()
|
  IfStatement()
|
  WhileStatement()
|
  DoStatement()
|
  ForStatement()
|
  BreakStatement()
|
  ContinueStatement()
|
  ReturnStatement()
|
  ThrowStatement()
|
  SynchronizedStatement()
|
  TryStatement()
}

void LabeledStatement() :
{
    Token tok;
    Token colon;
}
{
  tok=<IDENTIFIER> colon=":" Statement()
  {
    jjtThis.setName(tok.image);
    jjtThis.addSpecial("id", tok.specialToken);
    jjtThis.addSpecial("colon", colon.specialToken);
  }
}

void Block() :
{
    Token tok;
}
{
  tok="{" {jjtThis.addSpecial("begin", tok.specialToken);}
  ( BlockStatement() )*
  tok="}" {jjtThis.addSpecial("end", tok.specialToken);}
}

void BlockStatement() :
{
    Token tok;
    int count = 0;
}
{
  LOOKAHEAD( ( "final" | ("@" Annotation()) )* Type() <IDENTIFIER>)
  LocalVariableDeclaration()
  tok=";"
  {jjtThis.addSpecial("semicolon", tok.specialToken);}
|
  LOOKAHEAD( ( "final" | ("@" Annotation()) )* Name() "<" )
  LocalVariableDeclaration()
  tok=";"
  {jjtThis.addSpecial("semicolon", tok.specialToken);}
|
  Statement()
|
  LOOKAHEAD( ( "final" | ("@" Annotation()) )* "class")
  (
    (tok="@" Annotation() { jjtThis.addSpecial("@."+count, tok.specialToken); count++; }) |
    (tok="final" { jjtThis.addSpecial("final", tok.specialToken);  jjtThis.setFinal(true); } )
  )*
  UnmodifiedClassDeclaration()
|
  LOOKAHEAD([ "final" ] "interface")
  (
    (tok="@" Annotation() { jjtThis.addSpecial("@."+count, tok.specialToken); count++; }) |
    (tok="final" { jjtThis.addSpecial("final", tok.specialToken); jjtThis.setFinal(true); } )
  )*
  UnmodifiedInterfaceDeclaration()
}

void LocalVariableDeclaration() :
{
    Token tok = null;
    int commaCount = 0;
    int count = 0;
}
{
  ( 
    (tok="@" Annotation() { jjtThis.addSpecial("@."+count, tok.specialToken); count++; }) |
    (tok="final" { jjtThis.setUsingFinal(true); jjtThis.addSpecial("final", tok.specialToken); } )
  )*
  Type()
  VariableDeclarator()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken); commaCount++;}
    VariableDeclarator()
  )*
}

void EmptyStatement() :
{
    Token tok;
}
{
  tok=";"
  {jjtThis.addSpecial("semicolon", tok.specialToken);}
}

void StatementExpression() :
/*
 * The last expansion of this production accepts more than the legal
 * Java expansions for StatementExpression.  This expansion does not
 * use PostfixExpression for performance reasons.
 */
{
    Token tok;
}
{
  PreIncrementExpression()
|
  PreDecrementExpression()
|
  PrimaryExpression()
  [
    tok="++" {jjtThis.setName("++"); jjtThis.addSpecial("id", tok.specialToken);}
  |
    tok="--" {jjtThis.setName("--"); jjtThis.addSpecial("id", tok.specialToken);}
  |
    AssignmentOperator() Expression()
  ]
}

void SwitchStatement() :
{
    Token tok;
}
{
  tok="switch" {jjtThis.addSpecial("switch", tok.specialToken);}
  tok="(" {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  Expression()
  tok=")" {jjtThis.addSpecial("endExpr", tok.specialToken);}
  tok="{" {jjtThis.addSpecial("beginBlock", tok.specialToken);}
    ( SwitchLabel() ( BlockStatement() )* )*
  tok="}" {jjtThis.addSpecial("endBlock", tok.specialToken);}
}

void SwitchLabel() :
{
    Token tok;
    Token colon;
}
{
(
  tok="case" Expression() colon=":"
|
  tok="default" colon=":"
)
  {
    jjtThis.addSpecial("id", tok.specialToken);
    jjtThis.addSpecial("colon", colon.specialToken);
  }
}

void IfStatement() :
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{
    Token tok;
}
{
  tok="if" {jjtThis.addSpecial("if", tok.specialToken);}
  tok="(" {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  Expression()
  tok=")" {jjtThis.addSpecial("endExpr", tok.specialToken);}
  Statement()
  [
    LOOKAHEAD(1)
    tok="else" {jjtThis.setHasElse(); jjtThis.addSpecial("else", tok.specialToken);}
    Statement()
  ]
}

void WhileStatement() :
{
    Token tok;
}
{
  tok="while" {jjtThis.addSpecial("while", tok.specialToken);}
  tok="(" {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  Expression()
  tok=")" {jjtThis.addSpecial("endExpr", tok.specialToken);}
  Statement()
}


void ForStatement() :
{
    Token tok;
}
{
  tok="for" {jjtThis.addSpecial("for", tok.specialToken);}
  tok="(" {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  (
      
    LOOKAHEAD( {forLookahead()} )
    ( 
      LocalVariableDeclaration()
      tok=":" {jjtThis.addSpecial("loopover", tok.specialToken);}
      Expression()
    )
    |
    (
      [ ForInit() ]
      tok=";" {jjtThis.addSpecial("init", tok.specialToken);}
      [ Expression() ]
      tok=";" {jjtThis.addSpecial("test", tok.specialToken);}
      [ ForUpdate() ]
    )
  )

  tok=")" {jjtThis.addSpecial("endExpr", tok.specialToken);}
  Statement()
}

void ForInit() :
{}
{
  LOOKAHEAD( ( "final" | ("@" Annotation()) )* ( Type() <IDENTIFIER> | Name() "<" ) )
  (
    LocalVariableDeclaration()
  )
|
  (
    StatementExpressionList()
  )
}



void DoStatement() :
{
    Token tok;
}
{
  tok="do" {jjtThis.addSpecial("do", tok.specialToken);}
  Statement()
  tok="while" {jjtThis.addSpecial("while", tok.specialToken);}
  tok="(" {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  Expression()
  tok=")" {jjtThis.addSpecial("endExpr", tok.specialToken);}
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}



void StatementExpressionList() :
{
    Token tok;
    int commaCount = 0;
}
{
  StatementExpression()
  (
    tok="," {jjtThis.addSpecial("comma." + commaCount, tok.specialToken);  commaCount++;}
    StatementExpression()
  )*
}

void ForUpdate() :
{}
{
  StatementExpressionList()
}

void BreakStatement() :
{
    Token tok;
}
{
  tok="break" {jjtThis.addSpecial("break", tok.specialToken);}
  [ tok=<IDENTIFIER> {jjtThis.setName(tok.image); jjtThis.addSpecial("id", tok.specialToken);}]
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}

}

void ContinueStatement() :
{
    Token tok;
}
{
  tok="continue" {jjtThis.addSpecial("continue", tok.specialToken);}
  [ tok=<IDENTIFIER> {jjtThis.setName(tok.image); jjtThis.addSpecial("id", tok.specialToken);}]
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}

void ReturnStatement() :
{
    Token tok;
}
{
  tok="return" {jjtThis.addSpecial("return", tok.specialToken);}
  [ Expression() ]
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}

void ThrowStatement() :
{
    Token tok;
}
{
  tok="throw" {jjtThis.addSpecial("throw", tok.specialToken);}
  Expression()
  tok=";" {jjtThis.addSpecial("semicolon", tok.specialToken);}
}

void SynchronizedStatement() :
{
    Token tok;
}
{
  tok="synchronized"  {jjtThis.addSpecial("synchronized", tok.specialToken);}
  tok="("  {jjtThis.addSpecial("beginExpr", tok.specialToken);}
  Expression()
  tok=")"  {jjtThis.addSpecial("endExpr", tok.specialToken);}
  Block()
}

void TryStatement() :
/*
 * Semantic check required here to make sure that at least one
 * finally/catch is present.
 */
{
    Token tok;
    int catchCount = 0;
}
{
  tok="try"  {jjtThis.addSpecial("try", tok.specialToken);}
  Block()
  (
    tok="catch"  {jjtThis.setHasCatch(); jjtThis.addSpecial("catch" + catchCount, tok.specialToken);}
    tok="("  {jjtThis.addSpecial("beginExpr" + catchCount, tok.specialToken);}
    FormalParameter()
    tok=")"  {jjtThis.addSpecial("endExpr" + catchCount, tok.specialToken);}
    Block()
    {catchCount++;}
  )*
  [
    tok="finally"  {jjtThis.setHasFinally(); jjtThis.addSpecial("finally", tok.specialToken);}
    Block()
  ]
}

void AssertionStatement() :
{
    Token tok;
}
{
  tok = "assert" {
    if (!jdk1_4) {
      System.err.println("WARNING: assert declared in pre-jdk 1.4 target");
    }
    jjtThis.addSpecial("assert", tok.specialToken);
  }
  Expression()
  [
	  tok=":"  {jjtThis.addSpecial("colon", tok.specialToken);}
	  Expression()
  ]
  tok=";"
  {jjtThis.addSpecial("semicolon", tok.specialToken);
  }
}


